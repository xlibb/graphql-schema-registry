plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'
    id 'com.github.johnrengelman.shadow'
}

dependencies {
    checkstyle project(':checkstyle')
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstylePluginVersion}"

    implementation 'org.ballerinalang:ballerina-runtime:+'
    implementation "com.graphql-java:graphql-java:${graphqlJavaVersion}"
}

checkstyle {
    toolVersion "${project.checkstylePluginVersion}"
    configFile rootProject.file("build-config/checkstyle/build/checkstyle.xml")
    configProperties = ["suppressionFile" : file("${rootDir}/build-config/checkstyle/build/suppressions.xml")]
}

shadowJar {
    dependencies {
        include(dependency("com.graphql-java:graphql-java:${graphqlJavaVersion}"))
    }

    archiveClassifier.set('')
}
jar.enabled = false;
assemble.dependsOn(shadowJar);

checkstyleMain.dependsOn(":checkstyle:downloadCheckstyleRuleFiles")

spotbugsMain {
    effort "max"
    reportLevel "low"
    reportsDir = file("$project.buildDir/reports/spotbugs")
    reports {
        html.enabled true
        text.enabled = true
    }

    def excludeFile = file("${rootDir}/spotbugs-exclude.xml")
    if(excludeFile.exists()) {
        excludeFilter = excludeFile
    }

    def includeFile = file("${rootDir}/spotbugs-include.xml")
    if(includeFile.exists()) {
        includeFilter = includeFile
    }
}

spotbugsTest {
    enabled = false
}